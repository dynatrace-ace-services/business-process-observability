//DQL Request 3 - SLO calculation
fetch bizevents
| filter contains(event.type,  "online_betting")

// correlation based on runID
| summarize {
    records = if (
        isNotNull(takeAny(winners)), collectArray(record(betID,start_time=timestamp,winners=winners)),
        else: record(end_time=takeAny(timestamp),betID=takeAny(betID),winners=takeAny(winners)))
    }, by:{runID}    
| expand records

// correlation based on betID
| summarize { 
    startTime = takeAny(records[start_time]),runID = takeAny(runID),winners = takeAny(records[winners]), endTime = takeAny(records[end_time])
    }, by: {betID= records[betID]}

|fields start=toTimestamp(startTime), runID, betID, winners, end=toTimestamp(endTime), payment_delay=(toTimestamp(endTime)-toTimestamp(startTime))

//SLO Calculation
| summarize nbr_failed=countIf(payment_delay > 8m), total=count()
| fieldsAdd SLO=(toDouble(total - nbr_failed) * 100 / total), target=99
| fields SLO, target, SLO_success = if((SLO < target),false, else: true),total, nbr_failed
